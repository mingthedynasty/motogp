package com.season.classes.tooltip {

  import flash.display.*;
  import flash.text.*;
  import flash.geom.*;

  import com.season.utils.*;
  import com.season.classes.season.*;
  import com.season.models.*;

  /**********************
   *
   * RiderRaceTooltip
   *
   **********************/
  public class RiderRaceTooltip extends Tooltip {

    /**********************
     * VARIABLES
     **********************/
    private var raceLabel:TextField;
    private var positionLabel:TextField;
    private var pointsLabel:TextField;

    private var raceValue:TextField;
    private var positionValue:TextField;
    private var pointsValue:TextField;


    /**********************/
    static private var instance:RiderRaceTooltip = new RiderRaceTooltip();

    /**********************
     * CONSTRUCTOR
     **********************/
    public function RiderRaceTooltip() {
      super();

      //labels
      raceLabel = initLabel("RACE");
      positionLabel = initLabel("POSITION");
      pointsLabel = initLabel("POINTS");

      //values
      values.x = labels.getBounds(this).width + 10;
      raceValue = initValue();
      positionValue = initValue();
      pointsValue = initValue();
    }

    /**********************
     * PUBLIC
     **********************/
    public static function getInstance():RiderRaceTooltip  {      
      return instance;
    }

    /**********************/
    override public function show(ref:DisplayObject):void {
      var square:RacePointSquare = ref as RacePointSquare;
      var model:RiderStandingModel = square.model;

      //set values
      raceValue.text = model.raceModel.shortName;
      positionValue.text = String(model.racePlace);
      pointsValue.text = String(model.racePoints);

      layoutItems();
      super.show(ref);

      //figure out start/end pts
      var start = new Point(0, Math.round(height/2));
      var end = 
        new Point(-Math.round(ref.width/2) - 20,
                  start.y + Math.round(ref.height/2) + 15);
      
      var xoff = ref.width + 20;
      var yoff = -Math.round(height/2) - 15;

      //flip?
      if (xoff + ref.getBounds(parent).x + background.width > stage.stageWidth) {
        xoff = -(background.width + 20);
        start.x = background.width;
        end.x = background.width + 20 + Math.round(ref.width/2);
      }
      
      //finish
      finishShow(xoff, yoff, start, end);

    }

  }
}
